module AirMan
  ( Model
  , Action(NextFrame)
  , init
  , update
  , view
  , sampleKeyboardInput
  ) where

-- <MegaAirMan> Modules
import Shared exposing (Frame)

-- Elm Modules
import Effects exposing (Effects)
import Html exposing (Html, div)
import Html.Attributes exposing (classList)
import Keyboard
import Signal exposing (Signal)
import Time exposing (Time)

{-| This module controls all actions and all state associated with the Air Man
sprite. <Arena> initializes <AirMan>.

# Model
@docs ...

# Update
@docs ...

# view
@docs ...
-}


-----------
-- MODEL --
-----------

{-| Represents the 2D position of a sprite. <x> and <y> are the coordinate
position. <vx> and <vy> are the velocity along the axes. <dir> is the direction
this sprite faces.

    -- At any given moment in time, a jumping sprite may look like this.
    AirMan : Model
    AirMan =
      { ani = {...}
      , x = 0.0
      , y = 10.7
      , vx = 0.5
      , vy = 6.0
      , dir = Right
      }
-}
type alias Model =
  { ani : AnimationState
  , x : Float
  , y : Float
  , vx : Float
  , vy : Float
  , dir : Direction
  }


{-| This model holds all animation state associated with the Air Man sprite.
-}
type alias AnimationState =
  { previousTime : Time
  , elapsedTime : Time
  , currentFrame : Frame
  , stand : Frame
  , jump : Frame
  , shootTotalFrames : Int
  , shootF1 : Frame
  , shootF2 : Frame
  , shootF3 : Frame
  }


{-| Represents the direction this sprite faces within a 2D coordinate system.

    leftDirection : Direction
    leftDirection = Left
-}
type Direction
  = Left
  | Right


{-| Create the initial sprite model.
-}
init : (Model, Effects a)
init =
  Shared.noFx <|
  { ani =
      { previousTime = 0.0
      , elapsedTime = 0.0
      , currentFrame = "icon-mm2-airman-stand"
      , stand = "icon-mm2-airman-stand"
      , jump = "icon-mm2-airman-jump"
      , shootTotalFrames = 30 -- Cycle (F1 and F2) then play (F3).
      , shootF1 = "icon-mm2-airman-shoot1"
      , shootF2 = "icon-mm2-airman-shoot2"
      , shootF3 = "icon-mm2-airman-shoot3"
      }
  , x = 0.0
  , y = 0.0
  , vx = 0.0
  , vy = 0.0
  , dir = Right
  }


{-| Represents Signal output from the Keyboard module. Positive values indicate
up and right. Negative values indicate bottom and left. Zero is no input. See
<Keyboard> for further explanation.
-}
type alias ArrowKeys =
  { x : Int
  , y : Int
  }


{-| Represents a time measurement delta generated by the Signal section.
-}
type alias DeltaTime
  = Float


------------
-- UPDATE --
------------

{-| Trigger the next animation frame in the desired cycle with this Action type.
-}
type Action
  = HandleInput (DeltaTime, ArrowKeys)
  | NextFrame Time
  | Stand
  | Jump
  | Shoot


{-| The cloud background animation plays at about 3.5 FPS.
-}
playRate : Time
playRate =
  100 * Time.millisecond


{-| Updates state to cycle to the next animation frame.
-}
updateAnimationState : AnimationState -> Action -> Time -> AnimationState
updateAnimationState ani action clockTime =
  case action of
    Jump ->
      { ani | currentFrame = ani.jump }

    Shoot ->
      let
        -- Capture the total time this animation cycle has been playing.
        duration = ani.elapsedTime + (clockTime - ani.previousTime)

        -- Test whether the animation cycle has finished.
        newElapsedTime =
          if duration > (toFloat ani.shootTotalFrames) * playRate
          then 0.0
          else duration

        -- Get the next frame in the cycle.
        spin = toFloat ani.shootTotalFrames / 2

        nextFrame =
          if
            duration < spin * playRate && not (ani.currentFrame == ani.shootF1)
          then
            ani.shootF1
          else if
            duration < spin * playRate && not (ani.currentFrame == ani.shootF2)
          then
            ani.shootF2
          else
            ani.shootF3

      in
        { ani | previousTime = clockTime
              , elapsedTime = newElapsedTime
              , currentFrame = nextFrame
        }

    _ -> -- Default to the stand image.
      { ani | currentFrame = ani.stand }


{-| Update the Air Man sprite model in response to Signals and Effects.
The <NextFrame> action is initiated by <Arena>.
-- TODO Incorporate direction, translate movements, shoot timing
-}
update : Action -> Model -> (Model, Effects a)
update action model =
  Shared.noFx <|
  case action of
    NextFrame clockTime ->
      -- TODO Testing
      { model | ani = updateAnimationState model.ani Shoot clockTime }

    Jump ->
      { model | ani = updateAnimationState model.ani action 0.0 }

    Shoot ->
      { model | ani = updateAnimationState model.ani action 0.0 }

    _ ->
      { model | ani = updateAnimationState model.ani action 0.0 }


----------
-- VIEW --
----------

{-|
-}
view : Signal.Address Action -> Model -> Html
view address model =
  div -- Air Man sprite.
    [ classList
        [ ("abs", True)
        , ("btm", True)
        , ("icon", True)
        , (model.ani.currentFrame, True)
        , ("flip-x", model.dir == Right)
        ]
    ]
    [ -- Tornado sprites.
    ]


-------------
-- SIGNALS --
-------------

{-| Samples keyboard input to determine elapsed time and key presses.
-}
sampleKeyboardInput : Signal Action
sampleKeyboardInput =
  let
    delta = Signal.map (\t -> t/20) (Time.fps 30)
  in
    Signal.sampleOn delta (Signal.map2 (,) delta Keyboard.arrows)
    |> Signal.map (\(dt, kb) -> HandleInput (dt, kb))
